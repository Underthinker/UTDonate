import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.spongepowered.gradle.plugin' version '2.1.1'
}

dependencies {
    api project(':core')
    api("me.hsgamer:hscore-config-configurate:$core") {
        exclude group: 'org.spongepowered', module: 'configurate-core'
    }
    api("me.hsgamer:hscore-logging-log4j:$core") {
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
    }
}

sponge {
    apiVersion("8.0.0")
    license("Apache-2.0")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin(rootProject.name.toLowerCase()) {
        displayName(rootProject.name)
        entrypoint("io.github.underthinker.utdonate.sponge.UTDonateSponge")
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName = "${rootProject.name}-${project.version}-sponge.jar"

    relocate 'me.hsgamer.hscore', 'io.github.underthinker.utdonate.lib.hscore'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}